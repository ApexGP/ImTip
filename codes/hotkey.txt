import win.ui;
/*开发环境（请勿修改）{{*/
if(_STUDIO_INVOKED && ...!="ImTip"){
	var winform = win.form(text="超级热键";right=759;bottom=469)
	winform.add(
	edit={cls="edit";left=29;top=24;right=727;bottom=420;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;z=1}
	)
	winform.show();	
}
/*}}*/

/*
1. 保存此文件，自动更新 ImTip 热键配置。
2. 删除并关闭此文件，在 ImTip 中再次点击「编辑超级热键」可恢复默认配置。
教程: https://www.aardio.com/zh-cn/doc/?q=library-guide/std/key/hotkey.html
*/

import fsys;
import ide;
import winex; 
import win.dlg.chineseNumber;
import mouse;
import key;
import key.ime;
import key.hotkey;

//创建超级热键
superHotkey = key.hotkey();//不可修改或删除这句代码

superHotkey.loadTable({

	//大写金额与数字输入工具，支持算式表达式可作为简易计算器使用
	//如果要保留 Ctrl+4 请改为 Ctrl+Shift+$，不需要此热键可直接删除（或注释掉）
	["Ctrl+$"] = function(hFocus){  
		win.dlg.chineseNumber().show();
	};
	
	//如果要保留 Ctrl+2 请改为 Ctrl+Shift+@
	["Ctrl+@"] = function(){
		
		//获取选区文本
		import winex.selection;
		var selectedText = winex.selection.get(true);
		
		//选区文本为空，继续发送按键
		if(!#selectedText) return true;

		//去除首尾空格
		selectedText = string.trim(selectedText);
		if(!#selectedText) return true;

		//创建线程更流畅
		thread.invoke( 
			function(txt){
				import ide;//允许此线程调用 IDE 环境安装扩展库

				var words = string.splitEx(txt,"\s");
				if(#words>1 || (..string.find(txt,":") && ..string.len(txt)>4 ) ){ 
					
					//翻译 + 大声朗读
					import web.edgeTextToSpeech.translate; 
					var ttsForm = web.edgeTextToSpeech.translate(,txt) 
					ttsForm.text = "@AI 中英翻译 - 此页面支持英文划词速查";
					ttsForm.doModal();
				
					/*
					import process.imTip;
					process.imTip(
						chat = "翻译",// AI 助手配置名称
						q = "请翻译:" + txt
					)  
					*/
				}
				else{
					import win.dlg.flashDict;
					var flashDict = win.dlg.flashDict(,txt)
					flashDict.text = "@AI 中英词典";
					flashDict.doModal();;
				}	
			},selectedText);
	}
	
	//如果要保留 Ctrl+3 请改为 Ctrl+Shift+#
	["Ctrl+#"] = function(){
			
		import winex.tooltip;	
		import winex.selection;
		var selectedText = winex.selection.get(true);
		
		selectedText = selectedText ? string.trim(selectedText);
		if(!#selectedText) return true;
		
		//显示数值的中文格式，支持 k,m 后缀
		var num = string.match(selectedText,"^([+-]?0*\d[\d,]*\.?\d*<\s?[kKmM]>?)$")
		if(num){ 
			num = string.replace(num,"[\s,\_]","");
			num = string.replace(num,"<\s?[kK]>$","000");
			num = string.replace(num,"<\s?[mM]>$","000000");
			
			import string.chineseNumber;
			var zh = string.chineseNumber().number(num);
			
			winex.tooltip.balloon(zh);
			return; 
		}
		
		import string.words;
		import table.coca20000;
			
		//查单词 + 朗读
		if( string.words(selectedText,true) ){ //是否单词
		
			var meaning,word = table.coca20000(selectedText,true)
			if(meaning){
				winex.tooltip.balloon(meaning);
				
				..wmPlayer := ..com.CreateObject("WMPlayer.OCX"); 
				..wmPlayer.url = "https://dict.youdao.com/dictvoice?audio="+word+"&type=2"
			}
			
			return;
		}
	};
	
	// AI 补全热键已移除
})

import winex.candidate;
import key.number;

//中文模式支持数字后输入小数点等分隔符
superHotkey.onKeyDown = function(vk){
	
	if(key.getStateX("CTRL") || key.getStateX("ALT") ) return; 
		
	if( winex.candidate.visible() ) {
		superHotkey.prevKeyCode = null;
		return;
	} 
	
	var sep = key.number.getSeparator(vk);

	if(sep){ 
		if( key.number.is(superHotkey.prevKeyCode)){ 
			key.sendString( sep );
				
			return true;
		} 	
	} 
		
	if(vk!=0xA0/*_VK_LSHIFT*/ && vk!=0xA1/*_VK_RSHIFT*/){
		superHotkey.prevKeyCode = vk;	
	} 
}

//启动线程消息循环
win.loopMessage();//不可修改或删除这句代码
